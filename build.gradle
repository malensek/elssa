apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.netty', name: 'netty-all', version: '4.0.31.Final'
    compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.12'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
}

Map<String, String> jarAttributes = [
        'Implementation-Title': project.name,
        'Implementation-Vendor': project.vendor,
        'Implementation-Version': project.version,
        'Built-By': System.getProperty('user.name'),
        'Build-Jdk': System.getProperty('java.version'),
]

jar {
    manifest {
        attributes(jarAttributes)
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes(jarAttributes)
    }

    baseName = project.name + '-all'
    from configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it)
    }

    with jar
}

task testJar(type: Jar) {
    baseName = project.name + '-test'
    from sourceSets.test.output
}
